
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Сч = 1 По 3 Цикл
	
		СписокЗаданий.Добавить(ВыполнениеЗадания.ВыполнитьЗаданиеВФоне(Сч));
	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ЗаполнитьВыполнение", 2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыполнение() Экспорт 

	ПродолжитьВыполнение = Ложь;
	
	ЗаполнитьВыполнениеСервер(ПродолжитьВыполнение);
	
	Если ПродолжитьВыполнение Тогда
	
		ПодключитьОбработчикОжидания("ЗаполнитьВыполнение", 2, Истина);
	
	КонецЕсли; 

КонецПроцедуры // ()

&НаСервере
Процедура ЗаполнитьВыполнениеСервер(ПродолжитьВыполнение)

	Для каждого ЭлементСписка Из СписокЗаданий Цикл
	
		Задание = ПолучитьЗадание(ЭлементСписка.Значение);
		
		Если Задание = Неопределено Тогда
		
			Продолжить;
		
		КонецЕсли; 
		
		Сообщения = Задание.ПолучитьСообщенияПользователю(Истина);
		
		Для каждого Сообщение Из Сообщения Цикл
		
			Если ЗначениеЗаполнено(Лог) Тогда
			
				Лог = Лог + Символы.ПС;
			
			КонецЕсли; 
			
			Лог = Лог + Сообщение.Текст;
		
		КонецЦикла;
		
		Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
		
			ПродолжитьВыполнение = Истина;
		
		КонецЕсли; 
	
	КонецЦикла; 

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗадание(Идентификатор)

	Возврат ФоновыеЗадания.НайтиПоУникальномуИдентификатору(Идентификатор);

КонецФункции// ПолучитьЗадание()
 
 